#include "common.hlsl_psh"

sampler AlbedoSampler	: register(s0);
sampler NormalSampler	: register(s1);
sampler SpecSampler		: register(s2);
sampler OverlaySampler	: register(s3);

struct PS_INPUT 
{
	float2	UV			: TEXCOORD0;
	float4	PositionWS	: TEXCOORD1;
	float4	PositionSS	: TEXCOORD2;
	float3	NormalWS	: TEXCOORD3;
	float4	TangentWS	: TEXCOORD4;
};

struct PS_OUTPUT
{
	float4	Normal	: COLOR0;
	float4	Albedo	: COLOR1;
	float4	Depth	: COLOR2;
};

PS_OUTPUT Main( PS_INPUT In )
{
	PS_OUTPUT Out;

	// Spec map surface notes:
	// SpecMap.r: reflectance (diffuse-specular and ambient-cubemap lerp)
	// SpecMap.g: smoothness (specular power, mapped to [0,1])
	// SpecMap.b: emissive
	// SpecMap.a: metal/dielectric (binary)
	
	// G-buffer notes:
	// Normal.r: stereographic normal X
	// Normal.g: stereographic normal Y
	// Normal.b: smoothness + normal Z sign
	// Normal.a: reflectance + metal/dielectric

	// Renormalize normal and tangent before creating TBN matrix
	const float3	Normal		= normalize( In.NormalWS );
	const float3	Tangent		= normalize( In.TangentWS.xyz );
	const float3	Bitangent	= GetBitangent( Normal, Tangent, In.TangentWS.w );
	const float4x4	InvTBN		= GetInverseTBNMatrix( Tangent, Bitangent, Normal );
	
	// Flip some of the triplanar mapping UVs based on the normal so they project as expected,
	// if the overlay has text or needs to be aligned with the texture or whatever.
	// This works on walls, not so well on floors that could be oriented in any direction.
	float4			Signs		= float4( sign( Normal ), 1.0f );
	Signs.y						= -Signs.y;
	Signs.z						= -Signs.z;
	
	// Hard-coded scale and sharpness because I don't expect to use this much anyway.
	// Scale controls how the worldspace position maps; 4.0f means a texture spans 4m.
	// Sharpness controls how the triplanar mapping blends on angled normals.
	const float		OverlayScale	= 4.0f;
	const float		OverlaySharp	= 4.0f;	// Sharpened from 2.0f since I'm using PS2b now.
	float3			OverlayUVW		= In.PositionWS.xyz / OverlayScale;
	const float4	OverlayX		= tex2D( OverlaySampler, OverlayUVW.yz * Signs.xw );
	const float4	OverlayY		= tex2D( OverlaySampler, OverlayUVW.xz * Signs.yw );
	const float4	OverlayZ		= tex2D( OverlaySampler, OverlayUVW.xy * Signs.wz );
	float3			OverlayWeights	= pow( abs( In.NormalWS ), OverlaySharp );
	// Make weights sum to 1
	OverlayWeights					= OverlayWeights / ( OverlayWeights.x + OverlayWeights.y + OverlayWeights.z );
	const float4	Overlay			= OverlayWeights.x * OverlayX +
									  OverlayWeights.y * OverlayY +
									  OverlayWeights.z * OverlayZ;

	const float3	Albedo		= sRGBToLinear( tex2D( AlbedoSampler, In.UV ) * Overlay ).rgb;

	const float3	NormalMapTS	= SampleNormal( NormalSampler, In.UV );
	const float3	NormalMapWS	= mul( float4( NormalMapTS, 0.0f ), InvTBN ).xyz;

	const float4	SpecMap		= tex2D( SpecSampler, In.UV );
	const float2	ReflSmooth	= Pack( float2( SpecMap.r, SpecMap.g * SpecMap.g ) );	// Square for perceptually linear smoothness. ROSAHACK: Pack because we need these to be non-zero for multiplying with sign bits.
	const float		Emissive	= Pack( SpecMap.b );	// Pack for multiplying with skin bit
	const float		Dielectric	= SpecMap.a * 2.0f - 1.0f;	// ROSANOTE: Dielectric/metal is binary. -1 if metal, 1 if dielectric.

	const float		Depth		= In.PositionSS.z / In.PositionSS.w;

	const float		NormalZSign	= NonZeroSign( NormalMapWS.z );
	const float4	NormalPack	= float4( GetStereographicProjection( NormalMapWS ), NormalZSign * ReflSmooth.y, Dielectric * ReflSmooth.x ) * 0.5f + 0.5f;
	const float4	AlbedoPack	= float4( Albedo, Emissive * 0.5f + 0.5f );	// Emissive also stores skin bit (-1 if skin, 1 if not skin; this shader never emits skin)

	Out.Normal	= NormalPack;
	Out.Albedo	= AlbedoPack;
	Out.Depth	= float4( Depth, Depth, Depth, Depth );
	
	return Out;
}